plugins {
    id 'ru.vyarus.java-lib' version '1.0.4' apply false
    id 'ru.vyarus.github-info' version '1.1.0' apply false
    id 'ru.vyarus.quality' version '2.0.0' apply false
    id 'com.jfrog.bintray' version '1.7.1' apply false
    id 'com.github.ben-manes.versions' version '0.13.0' apply false
    id "io.spring.dependency-management" version "0.6.0.RELEASE" apply false

    id 'base'
    id 'jacoco'
    id 'net.researchgate.release' version '2.4.1'
    id 'com.github.kt3k.coveralls' version '2.6.3'
}

wrapper {
    gradleVersion = 3.0
}

description = 'Dropwizard-guicey extensions'

allprojects {
    apply plugin: 'project-report'
    apply plugin: 'com.jfrog.bintray'

    repositories { jcenter(); mavenCentral(); mavenLocal() }

    bintray {
        user = findProperty('bintrayUser')
        key = findProperty('bintrayKey')
        pkg {
            repo = 'xvik'
            name = rootProject.name
            desc = rootProject.description
            labels = ['dropwizard', 'guice']
            publicDownloadNumbers = true
        }
    }
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'jacoco'
    apply plugin: 'ru.vyarus.java-lib'
    apply plugin: 'ru.vyarus.quality'
    apply plugin: 'ru.vyarus.github-info'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8

    ext {
        dropwizard = '1.0.5'
        guicey = '4.0.1'
        spock = '1.0-groovy-2.4'
    }

    dependencyManagement {
        imports {
            mavenBom "io.dropwizard:dropwizard-bom:$dropwizard"
        }
        dependencies {
            dependency "ru.vyarus:dropwizard-guicey:$guicey"
            dependency "org.spockframework:spock-core:$spock"
        }
    }
    dependencies {
        provided 'com.google.code.findbugs:jsr305:3.0.1'
        provided 'com.google.code.findbugs:annotations:3.0.1'

        compile "ru.vyarus:dropwizard-guicey"

        testCompile "org.spockframework:spock-core"
        testCompile "io.dropwizard:dropwizard-testing"
    }

    group = 'ru.vyarus.guicey'

    github {
        user = 'xvik'
        repository = rootProject.name
        license = 'MIT'
        licenseUrl = github.rawFileUrl('LICENSE');
        changelogFile = 'CHANGELOG.md'
    }

    pom {
        developers {
            developer {
                id 'xvik'
                name 'Vyacheslav Rusakov'
                email 'vyarus@gmail.com'
            }
        }
    }

    bintray {
        publications = ['maven']
    }

    test {
        testLogging {
            events 'skipped', 'failed', 'standard_error'
            exceptionFormat 'full'
        }
        maxHeapSize = '512m'
    }
    jacocoTestReport.reports.xml.enabled = true

    dependencyUpdates.revision = 'release'

    task showDependenciesTree(dependsOn: 'htmlDependencyReport', group: 'help', description:
            'Generates dependencies tree report and opens it in browser') << {
        java.awt.Desktop.getDesktop().open(file('/build/reports/project/dependencies/root.html'))
    }
}

/*
 Each module published separately: subprojects simply upload files to bintray and root project will
 publish/generate signatures/sync with maven central.
 Note: plugin will find bintray tasks in submodules and depend on it so order will be always correct.
 */
bintray {
    publish = true
    pkg {
        version {
            gpg {
                sign = true
                passphrase = findProperty('gpgPassphrase')
            }
            mavenCentralSync {
                sync = false
                user = findProperty('sonatypeUser')
                password = findProperty('sonatypePassword')
            }
        }
    }
}

// dependency on all subprojects required for release validation
build.dependsOn subprojects.build

// release manages only root project (subprojects will be checked and released implicitly)
afterReleaseBuild {
    dependsOn = [bintrayUpload]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

task testReport(type: TestReport, description: 'Generates an aggregate test report') {
    destinationDir = file("${buildDir}/reports/tests")
    reportOn subprojects.test
}

task jacocoMerge(type: JacocoMerge, description: 'Merge coverage results for aggregated report generation') {
    executionData = files(subprojects.collect {it.file('build/jacoco/test.exec')})
    doFirst {
        executionData = files(executionData.findAll { it.exists() })
    }
}

task jacocoTestReport(type: JacocoReport, description: 'Generates aggregated coverage report') {
    dependsOn jacocoMerge
    executionData jacocoMerge.destinationFile
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(subprojects.sourceSets.main.output)
    reports.xml.enabled = true
}

coveralls {
    sourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
    jacocoReportPath = 'build/reports/jacoco/jacocoTestReport/jacocoTestReport.xml'
}

htmlDependencyReport.projects = allprojects

