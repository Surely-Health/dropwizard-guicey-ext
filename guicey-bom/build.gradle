description = "Guicey extensions BOM"

def props = ['dropwizard.version'           : dropwizard,
             'guicey.version'               : guicey,
             'spock.version'                : spock,
             'guice-ext-annotations.version': guiceExtAnn]

// main dependenciesManagement section will be generated based on dependenciesManagement declared in root project
withPomXml {
    it.appendNode('properties').with {
        props.each { nm, val -> appendNode(nm, val) }
    }

    Node deps = it.dependencyManagement.dependencies[0]

    // add subprojects to the bom
    project.rootProject.subprojects.each {
        if (it.name != 'guicey-bom') {
            Node dep = deps.appendNode('dependency')
            dep.appendNode('groupId').value = it.group
            dep.appendNode('artifactId').value = it.name
            dep.appendNode('version').value = it.version
        }
    }

    // bom does not need dependencies itself
    it.remove(it.dependencies[0])
}

publishing.publications.maven.artifacts = []